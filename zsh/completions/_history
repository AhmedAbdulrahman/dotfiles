#compdef history
# vim: ft=zsh

_history () {
    local -a _1st_arguments
    _1st_arguments=(
    'add:Add new history'
    'config:Config the setting file'
    'edit:Edit your history file directly'
    'list:List the history'
    'search:Search the command from the history file'
    'delete:Delete the record from history file'
    'sync:Sync the history file with gist'
    'help:Show help for any command'
    )

    _arguments \
        '(--help)--help[Show the help message]' \
        '(-v --version)'{-v,--version}'[Show the version and exit]' \
        '*:: :->subcmds' \
        && return 0

    if (( CURRENT == 1 )); then
        _describe -t commands "history subcommand" _1st_arguments
        return
    fi

    case "$words[1]" in
        (add)
            _arguments \
                '(- :)--help[Show this help and exit]' \
                '(--branch)--branch=[Set branch]' \
                '(--dir)--dir=[Set dir]' \
                '(--command)--command=[Set command]' \
                '(--status)--status=[Set status]' \
                && return 0
            ;;
        (search|list|delete)
            _arguments \
                '(- :)--help[Show this help and exit]' \
                '(--filter-branch -b)'{--filter-branch,-b}'['${(C)words[1]}' with branch]' \
                '(--filter-dir -d)'{--filter-dir,-d}'['${(C)words[1]}' with dir]' \
                '(--filter-hostname -p)'{--filter-hostname,-p}'['${(C)words[1]}' with hostname]' \
                '(--query -q)'{--query,-q}'=['${(C)words[1]}' with query]' \
                '(--columns -c)'{--columns,-c}'=['${(C)words[1]}' columns with options]' \
                && return 0
            ;;
        (sync)
            _arguments \
                '(- :)--help[Show this help and exit]' \
                '(--interval)--interval=[Sync with the interval]' \
                '(--diff)--diff=[Sync if the diff exceeds a certain number]' \
                '(--ask)--ask[Sync after the confirmation]' \
                && return 0
            ;;
        (edit)
            _arguments \
                '(- :)--help[Show this help and exit]' \
                && return 0
            ;;
        (config)
            keys=( $(command history config --keys) )
            _arguments \
                '(- :)--help[Show this help and exit]' \
                '(--get)--get[Get the config value]: :->keys' \
                '(--keys)--keys[Get the config keys]'
            case "$state" in
                (keys)
                    _describe -t keys 'toml keys' keys && return 0
                    ;;
            esac
            ;;
        (help)
            _values 'help message' ${_1st_arguments[@]%:*} && return 0
            ;;
    esac
}

_history "$@"
