#compdef add-sshkey-remote

local -a _1st_arguments
_1st_arguments=(
  'ssh:adding ssh keys to authorized_keys on remote host'
  'github:adding ssh keys to github'
  'bitbucket:adding ssh keys to bitbucket'
  'help:print this'
)

__users() {
  local -a _users
  _users=("$(git config --get user.name)" $USER)
  _wanted application expl 'users' compadd $_users
}

__hosts() {
  local -a _hosts
  _hosts=(${(R)${(@f)"$(<~/.ssh/known_hosts)"}%%[, ]*})
  _wanted application expl 'hosts' compadd $_hosts
}

local expl
local -a boxes installed_boxes

local curcontext="$curcontext" state line
local -A opt_args

_arguments -C \
  ':command:->command' \
  '(- :)'{-h,--help}'[Show this help and exit]' \
  '(-f --file)'{-f,--file}'[Display percentage with ansi]' \
  '(-)*::options:->options'

case $state in
  (command)
    _describe -t commands "services" _1st_arguments
    return
    ;;

  (options)
    case $line[1] in
      (ssh)
        _arguments ':hostname:__hosts'
        ;;
      (github|bitbucket)
        _arguments ':users:__users'
        ;;
    esac
    ;;
esac

# vim: ft=zsh sw=2 ts=2 et
